plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

repositories {
    mavenCentral()
}

ext {
    def env = System.getenv()
    buildNumber = env.BUILD_NUMBER
    local = buildNumber == null
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group = "org.bookmc"
version = "0.3.0+${buildNumber ? "build.$buildNumber" : "local"}"

gradlePlugin {
    plugins {
        buildtools {
            id = "org.bookmc.buildtools"
            implementationClass = "org.bookmc.tools.BuildTools"
        }
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    implementation gradleApi()

    include "com.google.code.gson:gson:2.8.8"
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "BookMC"
                    }
                }
            }
        }
    }
}