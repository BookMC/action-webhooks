plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

ext {
    def env = System.getenv()
    buildNumber = env.BUILD_NUMBER
    local = buildNumber == null
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group = "org.bookmc"
version = "0.1.1+${buildNumber ? "build.$buildNumber" : "local"}"

gradlePlugin {
    plugins {
        buildWebhooks {
            id = "org.bookmc.build-webhooks"
            implementationClass = "org.bookmc.build.BuildWebhooks"
        }
    }
}

dependencies {
    implementation "club.minnced:discord-webhooks:0.5.8"
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "BookMC"
                    }
                }
            }
        }
    }
}